/**
 * @author André Victor Teixeira Lopes
 * @author Bruno de Castro Brezolin
 * @author Melissa Wong
 */

options {
  //Opção necessária para debugar o parser
  DEBUG_LOOKAHEAD = true;
  //Codigo da classe não estático
  STATIC = false;
  //Alteração do nó da arvore para um nó criado pelo usuário
  NODE_CLASS = "NotSoSimpleNode";
}

/**
 * Inicio do código principal do Parser
 * (Nome da Classe principal)
 */
PARSER_BEGIN(LangGoTwo)
package parser;

import java.io.*;
import recovery.*;

/**
 * Classe principal da Linguagem LangGoTwo
 */
public class LangGoTwo {
  final static String Version = "GoTwo Compiler - Version 0.1 - 2018"; //String de versão da linguagem mostrada pelo compilador
  private int countParseError = 0; //Contador de erros do parser do compilador
  private boolean debug_recovery = false; //Indica modo de debug
  private boolean eof = false; //Indica se foi alcançado o fim do arquivo
  private Token lastError = null; //Indica o token em que ocorreu o último erro
  
  /**
   * Método Principal
   * 
   * @param args  Argumentos de entrada do programa principal
   */
  public static void main(String[] args) {
    String filename = "";
    LangGoTwo parser;
    boolean debug_AS = false;
    boolean debug_recovery = false;

    System.out.println(Version);

    int i;
    for (i = 0; i < args.length - 1; i++) {
      // Verificando argumentos de debug
      if (args[i].equals("-debug_AS") || args[i].toLowerCase().equals("-d")) {
        debug_AS = true;
      }
      // Verificando argumentos de debug
      if (args[i].equals("-debug_recovery") || args[i].toLowerCase().equals("-d")) {
        debug_recovery = true;
      }
      else { // Em caso de invocação incorreta do comando
        System.out.println("Uso correto: java LangGoTwo [-debug_AS], [-debug_recovery] ou [-d] inputfile");
        System.exit(0);
      }
    }

    if (args[i].equals('-')) { // Lê o programa a partir da entrada padrão
      System.out.println("Lendo da entrada padrão . . .");
      parser = new LangGoTwo(System.in);
    } 
    else { // Lê o programa a partir de arquivo
      filename = args[args.length - 1];
      System.out.println("Lendo do arquivo " + filename + " . . .");
      try {
        parser = new LangGoTwo(new java.io.FileInputStream(filename));
      } 
      catch (java.io.FileNotFoundException e) {
        System.out.println("Arquivo " + filename + " não encontrado.");
        return;
      }
    } 
    // Ativa ou desativa os debugs
    if (!debug_AS) parser.disable_tracing();
    parser.debug_recovery = debug_recovery;

    try {
      // Arvore Sintática recebe a chamada recursiva
      NotSoSimpleNode root = parser.program();
      // Imprime a árvore no terminal
      root.dump("");
    }
    catch (ParseException e) {
      System.err.println(e.getMessage());
    }
    finally {
      System.out.println(parser.token_source.foundLexError() + " erro(s) Léxicos encontrados.");
      System.out.println(parser.countParseError + " erro(s) Sintáticos encontrados.");
    }

  }

  /**
   * Auxilia a imprimir o token certo
   * 
   * @param x numero do token no JavaCC
   */
  static public String im(int x) {
    int k;
    String s;
    s = tokenImage[x];
    k = s.lastIndexOf("\"");
    try {
      s = s.substring(1, k);
    }
    catch (StringIndexOutOfBoundsException e) { }
    return s;
  }

  /**
   * Consome o token até achar um token de sincronização
   * 
   * @param rs  Tokens de sincronização
   * @param e   Exceção pegada pelo catch()
   * @param met Nome do não-terminal
   */
  public void consumeUntil(RecoverySet rs, ParseException e, String met) throws ParseException {
      Token tok;
      if (debug_recovery)
      {
        System.out.println();
        System.out.println("*** " + met + " ***");
        System.out.println("Sincronizando: " + rs);
      }

      if (rs == null)
        throw e;

      tok = getToken(1);
      while (!eof)
      {
        if (rs.contains(tok.kind))
        {
          if (debug_recovery)
          {
            System.out.println("Encontrado token de sincronização: " + im(tok.kind));
          }
          break;
        }
        if (debug_recovery)
        {
          System.out.println("Ignorando token: " + im(tok.kind));
        }
        getNextToken();
        tok = getToken(1);
        // System.out.println("TOK.KIND = " + im(tok.kind));
      if (tok.kind == EOF && !rs.contains(EOF)) eof = true;
    }

    if (tok != lastError) {
      System.out.println(e.getMessage());
      lastError = tok;
      countParseError++;
    }
    if (eof) {
      throw new ParseException("EOF encontrado antes do fim do arquivo.");
    }
  }

} //LangGoTwo

PARSER_END(LangGoTwo)

/**
 * Método que inicia a analise sintática, primeiro não terminal da Linguagem, 
 * com tratamento de erro sintático
 * 
 * @return NotSoSimpleNode arvore sintática construida por jjtree
 */
NotSoSimpleNode program(): { 
  RecoverySet g = First.program,
              f1 = new RecoverySet(SEMICOLON);
}
{
  <EOF> {return jjtThis;} |
  (
    try {
      <PACKAGE> ident(f1) <SEMICOLON>
      [ <IMPORT> importDecl(f1) <SEMICOLON> ]
      funcList(g) <EOF>  {return jjtThis;}
    } 
    catch (ParseException e) {
      consumeUntil(g, e, "program");
    }
  )
}

/**
 * Não-terminal de tipos da linguagem, com tratamento de erro sintático
 * 
 * @param g Tokens de sincronização
 */
void type(RecoverySet g): { Token t; }
{
  try {
    (t = <INT> | t = <STRING> | t = <BOOL> | t = <FLOAT> | t = <BYTE> | t = <RUNE>)
    { jjtThis.value = t.image; }
  } 
  catch(ParseException e) {
    consumeUntil(g, e, "type");
  }
}

/**
 * Não-terminal de declaração de import, com tratamento de erro sintático
 * 
 * @param g Tokens de sincronização
 */
void importDecl(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(RPAREN),
              f2 = First.importSpec.union(g);
}
{
  try {
    ( importSpec(f2) )+ | 
    <LPAREN> importSpec(f1) <RPAREN>
  } 
  catch(ParseException e) {
    consumeUntil(g, e, "importDecl");
  }
}

/**
 * Não-terminal
 */
void importSpec(RecoverySet g): { Token t; 
  RecoverySet f = new RecoverySet(STRING_LITERAL);
}
{
  try {
    [ ( <DOT> | ident(f) ) ] t = <STRING_LITERAL>
    { jjtThis.value = t.image; }
  }
  catch(ParseException e) {
    consumeUntil(g, e, "importSpec");
  }
}

/**
 *  Não-terminal da lista de funções
 *
 * @param g Tokens de sincronização
 */
void funcList(RecoverySet g): {
  RecoverySet f = First.funcList.union(g);
}
{
  (
    funcDecl(f) [ funcList(g) ]
  )
}

/**
 * Não-terminal de declaração de função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void funcDecl(RecoverySet g): { 
  RecoverySet f = First.funcBody;
}
{
  try {
    <FUNC> ident(f) funcBody(g)
  } 
  catch (ParseException e) {
    consumeUntil(g, e, "funcDecl");
  }
}

/**
 * Não-terminal de corpo de função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void funcBody(RecoverySet g): {
  RecoverySet f1 = new RecoverySet(RPAREN).union(g), 
              f2 = First.paramList.union(f1).remove(IDENT),
              f3 = First.statement.union(g).remove(IDENT);
}
{
  try {
    <LPAREN> (paramList(f2))* <RPAREN> [ LOOKAHEAD(2) funcReturnBody(f3) ] statement(g)
  }
  catch (ParseException e) {
    consumeUntil(g, e, "funcBody");
  }
}

/**
 * Não-terminal da especificação de retorno de uma função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void funcReturnBody(RecoverySet g): {
  RecoverySet f1 = new RecoverySet(RPAREN).union(g),
              f2 = First.returnList.union(f1);
}
{
  try {
    (<LPAREN> (returnList(f2))* <RPAREN>) | 
    (type(g))
  }
  catch (ParseException e) {
    consumeUntil(g, e, "funcReturnBody");
  }
}

/**
 * Não-terminal de lista de retorno de uma função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void returnList(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(COMMA).union(g),
              f2 = First.type.union(f1);
}
{
  try {
    [ ident(f2) ] type(f1) (<COMMA> [ ident(f2) ] type(f1))*
  }
  catch (ParseException e) {
    consumeUntil(g, e, "returnList");
  }
}

/**
 *  Não-terminal da lista de parâmetros de uma função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void paramList(RecoverySet g): { 
  RecoverySet f = First.paramLType;
}
{
  try {
    ident(f) paramLType(g)
  }
  catch (ParseException e) {
    consumeUntil(g, e, "paramList");
  }
}

/**
 * Não-terminal que representa o tipo de um parâmetro de uma função, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void paramLType(RecoverySet g): { 
  RecoverySet f = new RecoverySet(COMMA).union(g),
              f1 = First.type.union(f);
}
{
  try {
    LOOKAHEAD(2) type(f) (<COMMA> ident(f1) type(f))* | 
    (<COMMA> ident(f1))* type(g)
  }
  catch (ParseException e) {
    consumeUntil(g, e, "paramLType");
  }
}

/**
 * Não terminal que representa uma declaração de variável, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void varDecl(RecoverySet g): { 
  RecoverySet f = new RecoverySet(COMMA).union(g),
              f1 = First.type.union(f);
}
{
  try {
    <VAR> ident(f1) (<COMMA> ident(f1))* [type(g)]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "varDecl");
  }
}

/**
 * Não-terminal que representa uma declaração de variável constante, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void constDecl(RecoverySet g): { 
  RecoverySet f1 = First.type.union(g);
}
{
  try {
    <CONST> ident(f1) [type(g)]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "constDecl");
  }
}

/**
 * Não-terminal que rerpesenta uma expressão da linguagem, com tratamento de erro sintático
 *
 * @param g Tokens de sincronização
 */
void statement(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(SEMICOLON).union(g).remove(IDENT),
              f2 = new RecoverySet(RBRACE);
}
{
  try {
    funcDecl(g)
  | varDecl(f1) <SEMICOLON>
  | constDecl(f1) <SEMICOLON>
  | atribStat(f1) <SEMICOLON>
  | returnStat(f1) <SEMICOLON>
  | ifStat(g)
  | forStat(g)
  | <LBRACE> statList(f2) <RBRACE>
  | <BREAK> <SEMICOLON>
  | <SEMICOLON>
  }
  catch(ParseException e) {
    consumeUntil(g, e, "statement");
  }
}

/**
 * Não-terminal que define uma lista de expressões, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void statList(RecoverySet g): { 
  RecoverySet f = First.statList.union(g);
}
{
  try {
    [statement(f) statList(g)]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "statList");
  }
}

/**
 * Não-terminal que representa expressão de atribuição para uma variável, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void atribStat(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(ASSIGN).union(g),
              f2 = new RecoverySet(ASSINGTYPED).union(f1),
              f3 = First.expression.union(g).remove(IDENT);
}
{
  try {
    lvalue(f2) [assignValue(f3) expression(g) ]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "atribStat");
  }
}

/**
 * Não-terminal que representa o valor que vai ser atribuido a uma variável, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void assignValue(RecoverySet g): { Token t; }
{
  try {
    (t = <ASSIGN> | t = <ASSINGTYPED>)
    { jjtThis.value = t.image; }
  }
  catch (ParseException e) {
    consumeUntil(g, e, "assignValue");
  }
}

/**
 * Não-terminal que representa expressão de retorno de uma função, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void returnStat(RecoverySet g): {
  RecoverySet f1 = new RecoverySet(COMMA).union(g);
}
{
  try {
    <RETURN> [expression(f1) (<COMMA> expression(f1))*]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "returnStat");
  }
}

/**
 * Não-terminal que representa expressão de desvio condicinal "if", com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void ifStat(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(RPAREN),
              f2 = new RecoverySet(ELSE).union(g);
} 
{
  try {
    <IF> <LPAREN> expression(f1) <RPAREN> statement(f2)
    [LOOKAHEAD(1) <ELSE> statement(g)]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "ifStat");
  }
}

/**
 * Não-terminal que representa expressão de repetição "for", com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void forStat(RecoverySet g): { 
  RecoverySet f1 = new RecoverySet(SEMICOLON),
              f2 = new RecoverySet(RPAREN);
}
{
  try {
    <FOR> [ <LPAREN>  [atribStat(f1)] <SEMICOLON>
                      [expression(f1)] <SEMICOLON>
                      [atribStat(f2)] <RPAREN> ]
                      statement(g)
  }
  catch(ParseException e) {
    consumeUntil(g, e, "forStat");
  }
}

/**
 * Não-terminal que representa uma variável que vai receber uma atribuição, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void lvalue(RecoverySet g): { } 
{
  try {
    ident(null) [<LPAREN> argList(null) <RPAREN>]
    (
      <LBRACKET> expression(null) <RBRACKET> | 
      <DOT> ident(null) [<LPAREN> argList(null) <RPAREN>]
    )*
  }
  catch(ParseException e) {
    consumeUntil(g, e, "lvalue");
  }
}

/**
 * Não-terminal que representa uma expressão da linguagem, com tratamento de erro sintático
 *
 * @params g Tokens de sincronização
 */
void expression(RecoverySet g): { }
{
  try {
    numexpr() [ operator() numexpr()]
  }
  catch(ParseException e) {
    consumeUntil(g, e, "expression");
  }
}

/**
 * Não-terminal que representa um operador
 *
 * @params g Tokens de sincronização
 */
void operator(): { Token t; }
{
  ( t = <LT> | t = <GT> | t = <LE> | t = <GE> | t = <EQ> | t = <NEQ> )
  { jjtThis.value = t.image; }
}

/**
 * Não-terminal que representa uma expressão numérica
 *
 * @params g Tokens de sincronização
 */
void numexpr(): { }
{
  term() (( <PLUS> | <MINUS> ) term())*
}

/**
 * Não-terminal que representa um termo de uma expressão numérica
 *
 * @params g Tokens de sincronização
 */
void term(): { }
{
  unaryexpr() (( <STAR> | <SLASH> | <REM> ) unaryexpr())*
}

/**
 * Não-terminal que representa uma expressão unária
 *
 * @params g Tokens de sincronização
 */
void unaryexpr(): { }
{
  [(<PLUS> | <MINUS>)] factor()
}

/**
 * Não-terminal que representa parte de uma expressão
 *
 * @params g Tokens de sincronização
 */
void factor(): { Token t = null; }
{
  (
    t = <INTEGER_LITERAL> |
    t = <STRING_LITERAL> |
    t = <NULL_LITERAL> |
    t = <FLOATING_POINT_LITERAL> |
    lvalue(null) |
    <LPAREN> expression(null) <RPAREN>
  )
  { if (t != null) jjtThis.value = t.image; }
}

/**
 * Não-terminal que representa uma lista de argumentos de uma função
 *
 * @params g Tokens de sincronização
 */
void argList(RecoverySet g): { 
  RecoverySet f = new RecoverySet(COMMA).union(g);
}
{
  [ expression(f) ( <COMMA> expression(f) )* ]
}

/**
 * Não-terminal que representa um identificador
 *
 * @params g Tokens de sincronização
 */
void ident(RecoverySet g): { Token t; }
{
  try {
    t = <IDENT> { jjtThis.value = t.image; }
  }
  catch (ParseException e) {
    consumeUntil(g, e, "ident");
  }
}


TOKEN_MGR_DECLS:
{
  static int countLexErros = 0;

  public int foundLexError() {
    return countLexErros;
  }
}

SKIP:
{
  " "
| "\n"
| "\t"
| "\r"
| "\f"
}


/* Palavras Reservadas */

TOKEN:
{
  < BREAK: "break" >
| < FUNC: "func" >
| < IF: "if" >
| < ELSE: "else" >
| < FOR: "for" >
| < SWITCH: "switch" >
| < CASE: "case" >
| < INT: "int" >
| < STRING: "string" >
| < BOOL: "bool" >
| < BYTE: "byte" >
| < RUNE: "rune" >
| < FLOAT: "float" >
| < RETURN: "return" >
| < PACKAGE: "package" >
| < IMPORT: "import" >
| < VAR: "var" >
| < CONST: "const" >
}

/* Operadores */

TOKEN:
{
  < ASSIGN: "=" >
| < ASSINGTYPED: ":=" >
| < GT: ">" >
| < LT: "<" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NEQ: "!=" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < REM: "%" >
}

/* Simbolos Especiais */

TOKEN:
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

/* Constantes */

TOKEN: 
{
  < INTEGER_LITERAL: ( (["0"-"9"] (["0"-"9"])* ) ) >
| < STRING_LITERAL: "\""( ~["\"","\n","\r"])* "\"" >
| < NULL_LITERAL: "nil" >
| < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
| < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
}

/* Identificadores */

TOKEN:
{
  < IDENT: (<LETTER>) (<LETTER>|<DIGIT>|"_")* >
//| < RTYPES: <INT> | <STRING> | <BOOL> | <FLOAT> | <BYTE> | <RUNE> >
| < #LETTER: ( <LOWERLETTER> | <UPPERLETTER> ) >
| < #LOWERLETTER: ["a"-"z"] >
| < #UPPERLETTER: ["A"-"Z"] >
| < #DIGIT: ["0"-"9"] >
}

SKIP: 
{
  "/*": multicomment
| "//": singlecomment
}

<multicomment> SKIP:
{
  "*/": DEFAULT
| <~[]>
}

<singlecomment> SKIP:
{
  <["\n","\r"]>: DEFAULT
| <~[]>
}

/* TRATAMENTO DE ERROS LÉXICOS */

SPECIAL_TOKEN:
{
  <INVALID_LEXICAL:
  (~ [
    "A"-"Z","a"-"z",
    "0"-"9",
    "\"",
    "(",
    ")",
    "[",
    "]",
    "{",
    "}",
    ";",
    ",",
    ".",
    "=",
    ">",
    "<",
    "!",
    "+",
    "-",
    "*",
    "/",
    "%",
    " ",
    "\t",
    "\n",
    "\r",
    "\f"
  ] )+ >
  {
    System.out.println("Erro Léxico: ");
    System.out.println("linha " + input_stream.getEndLine() + ", coluna " + input_stream.getEndColumn() +
                        " - Token inválido encontrado: " + image);
    countLexErros++;
  }

|   <INVALID_CONST: "\"" ( ~["\n", "\r", "\""] )* ["\n", "\r"] >
  {
    System.out.println("Erro Léxico: ");
    System.out.println("linha " + input_stream.getEndLine() + ", coluna " + input_stream.getEndColumn() +
                        " - Constante String tem um \\n: " + image);
    countLexErros++;
  }
}